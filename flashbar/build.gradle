import static org.gradle.api.JavaVersion.VERSION_17

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

ext {
    publishedGroupId = 'com.andrognito.flashbar'
    libraryName = 'Flashbar'
    artifact = 'flashbar'

    libraryDescription = 'A highly customizable, powerful and easy-to-use alerting library for Android.'

    siteUrl = 'https://github.com/aritraroy/Flashbar'
    gitUrl = 'https://github.com/aritraroy/Flashbar.git'

    libraryVersion = '1.0.2'

    developerId = 'aritraroy'
    developerName = 'Aritra Roy'
    developerEmail = 'aritra.roy.in@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    namespace 'com.andrognito.flashbar'
    compileSdkVersion versions.sdkTarget

    defaultConfig {
        minSdkVersion versions.sdkMin
        targetSdkVersion versions.sdkTarget
        versionCode 3
        versionName "1.0.2"

        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility VERSION_17
        targetCompatibility VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$versions.supportVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += files(android.getBootClasspath())
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion

                from components.release
                artifact javadocJar

                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }

                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }

                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}
